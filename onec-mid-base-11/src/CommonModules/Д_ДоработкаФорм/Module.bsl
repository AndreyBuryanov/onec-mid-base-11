#Область ПрограммныйИнтерфейс

// Выводит элементы на форму
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой необходимо добавить элементы
Процедура ПриСозданииФормыНаСервере(Форма) Экспорт
	
	ИмяФормы = Форма.ИмяФормы; 
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеВвода(Форма, "КонтактноеЛицо", "Объект.Д_КонтактноеЛицо", Форма.Элементы.Найти("ГруппаШапкаПраво"));

		ГруппаСкидка = ДобавитьГруппу(Форма, "ГруппаСкидка", , Форма.Элементы.Найти("ГруппаШапкаЛево"));
		ПолеСкидка = ДобавитьПолеВвода(Форма, "СогласованнаяСкидка", "Объект.Д_СогласованнаяСкидка", ГруппаСкидка);
		ДобавитьКоманду(Форма, "Подключаемый_Д_ПересчитатьСуммы", "Пересчитать суммы", Истина, БиблиотекаКартинок.Обновить, ОтображениеКнопки.КартинкаИТекст);
		ДобавитьКнопку(Форма, "Подключаемый_Д_ПересчитатьСуммы", ВидКнопкиФормы.ОбычнаяКнопка, ГруппаСкидка);
		
		ПолеСкидка.УстановитьДействие("ПриИзменении", "Подключаемый_Д_СогласованнаяСкидкаПриИзменении");
		
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеВвода(Форма, "КонтактноеЛицо", "Объект.Д_КонтактноеЛицо", Форма.Элементы.Найти("ГруппаШапкаПраво"));
		
	ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеВвода(Форма, "КонтактноеЛицо", "Объект.Д_КонтактноеЛицо", Форма.Элементы.Найти("ГруппаШапкаПраво"));
		
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеВвода(Форма, "КонтактноеЛицо", "Объект.Д_КонтактноеЛицо", , Форма.Элементы.Найти("Основание"));
		
	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ДобавитьПолеВвода(Форма, "КонтактноеЛицо", "Объект.Д_КонтактноеЛицо", , Форма.Элементы.Найти("СуммаДокумента"));
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДобавитьПолеВвода(Форма, ИмяПоля, ПутьКДанным, Родитель = Неопределено, ПоместитьПеред = Неопределено)

	НовыйЭлемент = Форма.Элементы.Найти(ИмяПоля);
	
	Если НовыйЭлемент = Неопределено Тогда
		
		НовыйЭлемент = Форма.Элементы.Добавить(ИмяПоля, Тип("ПолеФормы"), Родитель);
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
		НовыйЭлемент.ПутьКДанным = ПутьКДанным;
		
		Если ПоместитьПеред <> Неопределено Тогда
			Форма.Элементы.Переместить(НовыйЭлемент, НовыйЭлемент.Родитель, ПоместитьПеред);
		КонецЕсли;
	КонецЕсли;
	
	Возврат НовыйЭлемент;
	
КонецФункции

Функция ДобавитьГруппу(Форма, ИмяГруппы, Группировка = Неопределено, Родитель = Неопределено, ПоместитьПеред = Неопределено)
	
	НовыйЭлемент = Форма.Элементы.Найти(ИмяГруппы);
	
	Если Группировка = Неопределено Тогда
		Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	КонецЕсли;
	
	Если НовыйЭлемент = Неопределено Тогда
		НовыйЭлемент = Форма.Элементы.Добавить(ИмяГруппы, Тип("ГруппаФормы"), Родитель);
		НовыйЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		НовыйЭлемент.Отображение = ОтображениеОбычнойГруппы.Нет;
		НовыйЭлемент.ОтображатьЗаголовок = Ложь;
		НовыйЭлемент.Группировка = Группировка;
	КонецЕсли;
	
	Возврат НовыйЭлемент;
	
КонецФункции

Функция ДобавитьКоманду(Форма, ИмяКоманды, Заголовок, ИзменяетСохраняемыеДанные, Картинка = Неопределено, Отображение = Неопределено)
	
	НоваяКоманда = Форма.Команды.Найти(ИмяКоманды);
	
	Если НоваяКоманда = Неопределено Тогда
		НоваяКоманда = Форма.Команды.Добавить(ИмяКоманды);
		НоваяКоманда.Действие = ИмяКоманды;
		НоваяКоманда.Заголовок = Заголовок;
		НоваяКоманда.Подсказка = Заголовок;
		НоваяКоманда.ИзменяетСохраняемыеДанные = ИзменяетСохраняемыеДанные;
		
		Если Картинка <> Неопределено Тогда
			НоваяКоманда.Картинка = Картинка;
		КонецЕсли;
		
		Если Отображение <> Неопределено Тогда
			НоваяКоманда.Отображение = Отображение;
		КонецЕсли;
	КонецЕсли;
	
	Возврат НоваяКоманда;
	
КонецФункции

Функция ДобавитьКнопку(Форма, ИмяКоманды, ВидКнопки, Родитель = Неопределено) Экспорт
	
	НовыйЭлемент = Форма.Элементы.Найти(ИмяКоманды);
	
	Если НовыйЭлемент = Неопределено Тогда
		НовыйЭлемент = Форма.Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), Родитель);
		НовыйЭлемент.Вид = ВидКнопки;
		НовыйЭлемент.ИмяКоманды = ИмяКоманды;
	КонецЕсли;
	
	Возврат НовыйЭлемент;
	
КонецФункции

#КонецОбласти
